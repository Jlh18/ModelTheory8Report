In this section and the next we focus on proving internal
completeness and soundness results:
\begin{prop}[Internal completeness and soundness]
  \link{algebraic_objects_iff_models}
  The following are true
  \begin{itemize}
    \item A type $A$ is a ring (according to lean) if and only if
          $A$ is a structure in the language of rings
          that models the theory of rings.
    \item A type $A$ is a field (according to lean) if and only if
          it is a model of the theory of fields.
    \item A type $A$ is an algebraically closed field (of characteristic p)
          if and only if it is a model of $\ACF_{(p)}$.
  \end{itemize}
  For the purposes of design in lean it is
  more sensible to split each ``if and only if'' into seperate constructions,
  for converting the algebraic objects into their model theoretic counterparts
  and vice versa.
  Although rather trivial on paper,
  converting between these facts takes a bit of work in \texttt{lean},
  especially for case of $\ACF_{p}$,
  where some ground work needs to be done for interpreting \texttt{gen\_monic\_poly}.
\end{prop}

Before we embark on a proof, we list some relevant facts and tips:
\begin{itemize}
  \item Proofs are easier when working in models, so our proofs tend to
        first translate everything we can to the ring,
        then prove the property there,
        making use of existing lemmas in the library for rings.
  \item An important instance of the above phenomenon is the lack
        of algebraic properties in the type \texttt{bounded\_ring\_terms}.
        For example, addition for polynomials written as terms
        is \textit{not commutative} until it is interpreted into a structure
        satisfying commutativity,
        even though it is true in a polynomial ring.
  \item Sometimes there is extra definitional rewriting that needs to happen,
        and \texttt{dsimp} (or something similar) is needed alongside \texttt{simp}.
\end{itemize}

\subsection{Ring Structures}

We first make the very obvious observation that given
the \texttt{lean} instances of \texttt{[has\_zero]} and \texttt{[has\_one]}
in some type \texttt{A},
we can make interpretations of the symbols \texttt{ring\_consts.zero}
and \texttt{ring\_consts.one}.
Similarly for the other symbols:

\begin{lstlisting}
def const_map [has_zero A] [has_one A] : ring_consts → dvector A 0 → A
| ring_consts.zero _ := 0
| ring_consts.one  _ := 1

def unaries_map [has_neg A] : ring_unaries → (dvector A 1) → A
| ring_unaries.neg a := - (dvector.last a)

-- Induction on both ring_binaries and dvector
def binaries_map [has_add A] [has_mul A] : ring_binaries → (dvector A 2) → A
| ring_binaries.add (a :: b) := a + dvector.last b
| ring_binaries.mul (a :: b) := a * dvector.last b

def func_map [has_zero A] [has_one A] [has_neg A] [has_add A] [has_mul A] :
  Π (n : ℕ), (ring_funcs n) → (dvector A n) → A
| 0       := const_map
| 1       := unaries_map
| 2       := binaries_map
| (n + 3) := pempty.elim\end{lstlisting}

This allows us to make any type with such instances a ring structure:

\begin{lstlisting}
def Structure : Structure ring_signature :=
Structure.mk A func_map (λ n, pempty.elim)\end{lstlisting}

Conversely given any ring structure,
we can easily pick out the above instances.
For example
\begin{lstlisting}
def add {M : Structure ring_signature} (a b : M.carrier) : M.carrier := @Structure.fun_map _ M 2 ring_binaries.add ([a , b])

instance : has_add M := ⟨ add ⟩\end{lstlisting}

\subsection{Rings}
If $A$ is a ring, then surely it is a model of the theory of rings.
I have supplied \texttt{simp} with enough lemmas to reduce the definitions
until requiring the corresponding property about rings,
and I have chosen the sentences to replicate the format of
each property from \texttt{mathlib}.
For example \texttt{add\_comm} below
is the internal property for the type \texttt{A}
(it is not visible to \texttt{simp}),
and it looks exactly like the statement $\texttt{M} \vDash \texttt{add\_comm}$.

\begin{lstlisting}
variables (A : Type*) [comm_ring A]

lemma realize_ring_theory :
  (struc_to_ring_struc.Structure A) ⊨ ring_signature.ring_theory :=
begin
  intros ϕ h,
  repeat {cases h},
  { intros a b c, simp [add_assoc] },
  { intro a, simp }, -- add_zero
  { intro a, simp }, -- add_left_neg
  { intros a b, simp [add_comm] },
  { intros a b c, simp [mul_assoc] },
  { intro a, simp [mul_one] },
  { intros a b, simp [mul_comm] },
  { intros a b c, simp [add_mul] }
end
\end{lstlisting}

Conversely, given a model of the theory of rings
we can supply an instance of a ring to the carrier type.
I supply a lemma for each piece of data going into a \texttt{comm\_ring}.
As an example, we look at \texttt{add\_comm}.

\begin{lstlisting}
/- First show that add_comm is in ring_theory -/
lemma add_comm_in_ring_theory : add_comm ∈ ring_theory :=
begin apply_rules [set.mem_insert, set.mem_insert_of_mem] end\end{lstlisting}

Since \texttt{ring\_theory} was just built as
\texttt{\{\_,\_,...,\_\}} (syntax sugar for
insert, insert, ..., singleton), it suffices just to iteratively
try a couple of lemmas for membership of such a construction.

\begin{lstlisting}
lemma add_comm (a b : M) (h : M ⊨ ring_signature.ring_theory) : a + b = b + a :=
begin
  /- M ⊨ ring_theory -> M ⊨ add_comm -/
  have hId : M ⊨ ring_signature.add_comm := h ring_signature.add_comm_in_ring_theory,
  /- M ⊨ add_comm -> add_comm b a -/
  have hab := hId b a,
  simpa [hab]
end\end{lstlisting}

There is some definitional and internal simplification happening in here,
but like before, for the most part \texttt{lean} recogizes that
realizing the sentence \texttt{add\_comm} is the same as having
an instance of \texttt{add\_comm}.

\begin{lstlisting}
def comm_ring (h : M ⊨ ring_signature.ring_theory) : comm_ring M :=
{
  add            := add,
  add_assoc      := add_assoc h,
  zero           := zero,
  zero_add       := zero_add h,
  add_zero       := add_zero h,
  neg            := neg,
  add_left_neg   := left_neg h,
  add_comm       := add_comm h,
  mul            := mul,
  mul_assoc      := mul_assoc h,
  one            := one,
  one_mul        := one_mul h,
  mul_one        := mul_one h,
  left_distrib   := mul_add h,
  right_distrib  := add_mul h,
  mul_comm       := mul_comm h,
}\end{lstlisting}

We make use of lean's type class inference system by making
the hypothesis of modelling \texttt{ring\_theory} an instance
using \texttt{fact}.

\begin{lstlisting}
instance models_ring_theory_to_comm_ring {M : Structure ring_signature}
  [h : fact (M ⊨ ring_signature.ring_theory)] : comm_ring M :=
models_ring_theory_to_comm_ring.comm_ring h.1 \end{lstlisting}

This way, we can supply an instance that any model of the theory of fields
(as a \texttt{fact}) is a model of the theory of ring (as a \texttt{fact}),
and is therefore a commutative ring.
We can then extend this commutative ring to a field.

\subsection{Fields}

Our characterization of fields resembles the structure \texttt{is\_field}
more than the default \texttt{field} instance;
they are equivalent.

\begin{lstlisting}
structure is_field (R : Type u) [ring R] : Prop :=
(exists_pair_ne : ∃ (x y : R), x ≠ y)
(mul_comm : ∀ (x y : R), x * y = y * x)
(mul_inv_cancel : ∀ {a : R}, a ≠ 0 → ∃ b, a * b = 1)\end{lstlisting}

The proof that any field forms a model of the theory of fields is straight forward:
since fields are commutative rings, it is a model of \texttt{ring\_theory}
by our previous work; for the other two sentences we exploit \texttt{simp}
and all the lemmas about fields that already exist in \texttt{mathlib}.

\begin{lstlisting}
lemma realize_field_theory :
  Structure K ⊨ field_theory :=
begin
  intros ϕ h,
  cases h,
  {apply (comm_ring_to_model.realize_ring_theory K h)},
  repeat {cases h},
   { intro,
     simp only [fol.bd_or, models_ring_theory_to_comm_ring.realize_one,
       struc_to_ring_struc.func_map, fin.val_zero', realize_bounded_formula_not,
       struc_to_ring_struc.binaries_map, fin.val_eq_coe, dvector.last,
       realize_bounded_formula_ex, realize_bounded_term_bd_app,
       realize_bounded_formula, realize_bounded_term,
       fin.val_one, dvector.nth, models_ring_theory_to_comm_ring.realize_zero],
     apply is_field.mul_inv_cancel (K_is_field K) },
  { simp [fol.realize_sentence] },
  end\end{lstlisting}

Going backwards is even easier.
We prove that any model of \texttt{field\_theory}
is a model of \texttt{ring\_theory} and therefore inherits a \texttt{comm\_ring} instance.
Given this instance of \texttt{comm\_ring},
it then makes sense to ask for a proof of \texttt{is\_field M},
which is straightforward:

\begin{lstlisting}
variables {M : Structure ring_signature} [h : fact (M ⊨ field_theory)]

include h

instance ring_model : fact (M ⊨ ring_theory) :=
⟨ all_realize_sentence_of_subset h.1 ring_theory_sub_field_theory ⟩

lemma zero_ne_one : (0 : M) ≠ 1 :=
by { have h1 := h.1, have h2 := h1 non_triv_in_field_theory, simpa [h2] }

lemma mul_inv (a : M) (ha : a ≠ 0) : (∃ (b : M), a * b = 1) :=
by { have h1 := h.1, have hmulinv := h1 mul_inv_in_field_theory, by simpa using hmulinv a ha }

lemma is_field : is_field M :=
{ exists_pair_ne := ⟨ 0 , 1 , zero_ne_one ⟩,
  mul_comm := mul_comm,
  mul_inv_cancel := mul_inv }

noncomputable instance field : field M :=
is_field.to_field M is_field \end{lstlisting}

\subsection{Algebraically closed fields}

Suppose we have an algebraically field \texttt{K}.
We want to show that it is a model of the theory of algebraically closed fields,
which given our work so far amounts to showing that for each natural number $n$
we have that all generic monic polynomials of degree $n$ have a root in \texttt{k}.
Indeed using \texttt{is\_alg\_closed} we can obtain such a root for any polynomial,
but this requires (internally) making a polynomial corresponding \texttt{gen\_monic\_poly n}.
We first assume the existence of such a polynomial \texttt{P}
and that evaluating such a polynomial
at some value \texttt{x} is the same thing as realising \texttt{gen\_monic\_poly n}
at (its coefficients and then) \texttt{x}.

\begin{lstlisting}
/-- Algebraically closed fields model the theory ACF-/
lemma realize_ACF : Structure K ⊨ ACF :=
begin
  intros ϕ h,
  cases h,
  /- we have shown that K models field_theory -/
  { apply field_to.realize_field_theory _ h },
  { cases h with n hϕ,
    rw ← hϕ,
    /- goal is now to show that all generic monic polynomials of degree n have a root -/
    simp only [all_gen_monic_poly_has_root, realize_sentence_bd_alls,
      realize_bounded_formula_ex, realize_bounded_formula,
      models_ring_theory_to_comm_ring.realize_zero],
    intro as,
    have root := is_alg_closed.exists_root
      (polynomial.term_evaluated_at_coeffs as (gen_monic_poly n)) gen_monic_poly_non_const,
      -- the above is our polynomial P and a proof that it is non-constant
    cases root with x hx,
    rw polynomial.eval_term_evaluated_at_coeffs_eq_realize_bounded_term at hx,
    -- the above is the lemma that evaluating P at x is the same as realizing gen_monic_poly n at x
    exact ⟨ x , hx ⟩ },
  end\end{lstlisting}

In order to interpret \texttt{gen\_monic\_poly n} as a polynomial,
we first note that it is natural to consider $n$-variable terms in the language of
rings as $n$-variable polynomials over $\Z$:

\begin{lstlisting}
def mv_polynomial.term {n} :
  bounded_ring_term n → mv_polynomial (fin n) ℤ :=
@ring_term_rec n (λ _, mv_polynomial (fin n) ℤ)
  mv_polynomial.X /- variable x_ i -> X i-/
  0 /- zero -/
  1 /- one -/
  (λ _ p, - p) /- neg -/
  (λ _ _ p q, p + q) /- add -/
  (λ _ _ p q, p * q) /- mul -/\end{lstlisting}

I designed a handy function called \texttt{ring\_term\_rec} that does
``induction on terms in the language of rings'',
based on \texttt{bounded\_term.rec} from the \texttt{flypitch} project.
This says that in order to make a multi-variable polynomial in variables $n$
over $\Z$ (\texttt{mv\_polynomial (fin n) $\Z$}) we can just case on the term.
If the term is a variable \texttt{x\_ i} for some $i < n$
then we interpret that as the polynomial $X_{i} \in \Z[X_{0},\dots,X_{n-1}]$.
The only other way we can get terms is by applying function symbols to other terms,
hence we interpret the symbols for zero and one as $0$ and $1$,
the symbolic negation of a term by subtracting the inductively given polynomial
for the term in the ring, and so on.

Then we use this to make an general algorithm that takes
a term $t$ in the language of rings with up to $n + 1$ variables
and a list of $n$ coefficients from a ring $A$,
and returns a polynomial in $A[X]$.
This is designed to treat he first variable $X_{0}$ of the associated polynomial
as the polynomial variable $X$,
and use the list (\texttt{dvector}) of coefficients
$[a_{1},\dots,a_{n}]$ to evaluate the variables $X_{1},\dots,X_{n}$.

\begin{lstlisting}
def polynomial.term_evaluated_at_coeffs {n} (as : dvector A n) (t : bounded_ring_term n.succ) : polynomial A :=
/- First make a map σ : {0, ..., n} → {X, as.nth' 0, ..., as.nth n} ⊆ A[X] -/
let σ : fin n.succ → polynomial A :=
@fin.cases n (λ _, polynomial A) polynomial.X (λ i, polynomial.C (as.nth' i)) in
/- Then this induces a map mv_polynomial.eval σ : A[X_0, ..., X_n] → A[X] by evaluating coefficients -/
mv_polynomial.eval
  σ
  (mv_polynomial.term t)
/- We evaluate at the multivariable polynomial corresponding to the term t -/
\end{lstlisting}

It remains to show that this polynomial in $A[X]$
evaluated at some $a_{0}$ gives the same value in the ring as the original term,
realized at the \texttt{dvector} $[a_{0},\dots,a_{n}]$.
This follows from the following two facts:
\begin{itemize}
  \item A term \texttt{t} realized at values $[a_{0},\dots,a_{n}]$ is equal to
        the polynomial \texttt{mv\_polynomial.term t}
        evaluated at the values $[a_{0},\dots,a_{n}]$.
        I called this \texttt{realized\_term\_is\_evaluated\_poly}
        and has a quick proof using \texttt{ring\_term\_rec}.
  \item If a multi-variable polynomial is evaluated at $(X,a_{1}\dots,a_{n})$
        in $A[X]$, then the resulting polynomial is evaluated at $a_{0}$,
        then this is the same as simply evaluating the multi-variable polynomial
        at $(a_{0},\dots,a_{n})$.
        This has a rather uninteresting proof,
        which I called \texttt{mv\_polynomial. eval\_eq\_poly\_eval\_mv\_coeffs}.
\end{itemize}

Moving on to the converse, we assume we have a model \texttt{M} of the theory
of algebraically closed fields, and a non-constant polynomial $p$
with coefficients in the model (as a field, by our previous work).
We want to show that $p$ has a root.
\begin{lstlisting}
variables {M : Structure ring_signature} [hM : fact (M ⊨ ACF)]

instance is_alg_closed : is_alg_closed M :=
begin
  apply is_alg_closed.of_exists_root_nat_degree,
  intros p hmonic hirr hdeg,
  sorry,
end\end{lstlisting}

We can feed the coefficients of $p$ to our model theoretic hypothesis,
which will give us a root to \texttt{gen\_monic\_poly}
realized at these coefficients, which I call \texttt{root}.

\begin{lstlisting}
instance is_alg_closed : is_alg_closed M :=
begin
  apply is_alg_closed.of_exists_root_nat_degree,
  intros p hmonic hirr hdeg,
  simp only [...] at hM,
  obtain ⟨ _ , halg_closed ⟩ := hM.1,
  set n := polynomial.nat_degree p - 1 with hn,
  /- I call the coefficients xs -/
  set xs := dvector.of_fn (λ (i : fin (n + 1)), polynomial.coeff p i) with hxs
  obtain ⟨ root , hroot ⟩ := halg_closed n xs,
  use root, /- root should be the root of p -/
  convert hroot,
  sorry,
end
\end{lstlisting}

It suffices to show that \texttt{root} is the root of $p$.
Given the hypotheses, this amounts to equating the (internal) algebraic goal
and the model theoretic hypothesis \texttt{hroot} about \texttt{root}.

\begin{lstlisting}
/- The goal (at 'convert hroot') -/
polynomial.eval root p = realize_bounded_term (root::xs) (gen_monic_poly n) dvector.nil\end{lstlisting}

In order to do this we \textit{could} try to reconstruct $p$
using our previous construction \texttt{polynomial. term\_evaluated\_at\_coeffs}.
However, unfortunately I have discovered that generally it can be
more straightforward to simply develop each side of the argument
(interanal completeness and soundness) seperately.
I make use of a result in the library that writes a polynomial
evaluated at a root as a sum indexed by its degree:

\begin{lstlisting}
lemma eval_eq_finset_sum (p : R[X]) (x : R) :
  p.eval x = ∑ i in range (p.nat_degree + 1), p.coeff i * x ^ i :=
/- See mathlib. -/
\end{lstlisting}

Then we can directly compare this to \texttt{gen\_monic\_poly} realized
at the values \texttt{xs} and \texttt{root}.
After providing \texttt{simp} with the appropriate lemmas
(such as the assumption that \texttt{p} is monic),
the goal reduces to

\begin{lstlisting}
root ^ p.nat_degree + (finset.range p.nat_degree).sum (λ (x : ℕ), p.coeff x * root ^ x) =
    root ^ p.nat_degree + realize_bounded_term (root::dvector.of_fn (λ (i : fin (n + 1)), p.coeff ↑i)) (gen_poly n) dvector.nil\end{lstlisting}

The first monomial pops out on both sides,
allowing us to cancel them with \texttt{congr}.
It remains to find out how \texttt{gen\_poly n} is realised.
We extract this as a lemma,
which we prove by induction on $n$, since \texttt{gen\_poly} was built inductively.
Each part is just a long \texttt{simp} proof which can be found in the source code.

\begin{lstlisting}
lemma realize_gen_poly {n : ℕ} {root} {c : ℕ → M} :
realize_bounded_term
  (dvector.cons root (dvector.of_fn (λ (i : fin (n + 1)), c i)))
  (gen_poly n) dvector.nil =
(finset.range (n + 1)).sum (λ (x : ℕ), c x * root ^ x) :=
begin
  induction n with n hn,
  { simp ... },
  { simp ... }
end\end{lstlisting}

\subsection{Characteristic}

We omit the details of similar proofs for characteristic as it is not
as interesting as the other parts.
Here are the lemmas we prove along the way,
some of which are convenient to feed to \texttt{lean} as \texttt{instances}

\begin{lstlisting}
instance models_ACFₚ_to_models_ACF [hp : fact (nat.prime p)] [hM : fact (M ⊨ ACFₚ hp.1)] : fact (M ⊨ ACF) := sorry

instance models_ACF₀_to_models_ACF [hM : fact (M ⊨ ACF₀)] : fact (M ⊨ ACF) := sorry

lemma models_ACFₚ_char_p [hp : fact (nat.prime p)] [hM : fact (M ⊨ ACFₚ hp.1)] : char_p M p := sorry

lemma models_ACF₀_char_zero' [hM : fact (M ⊨ ACF₀)] : char_zero M := sorry

\end{lstlisting}
