In this section we will introduce both \linkto{ax_groth_thm}{Ax-Grothendieck} and
its model theoretic counterpart.
We then investigate internal completeness and soundness for these statements.
We state the lemmas used in its proof, and use them to prove it.

\begin{dfn}[Polynomial maps]
  \link{dfn_poly_map}
  Let $K$ be a commutative ring and $n$ a natural
  (we use $K$ since we are only interested in the case
  when it is an algebraically closed field).
  Let $f : K^n \to K^n$ such that for each $a \in K^n$,
  \[f(a) = (f_1(a), \dots, f_n(a))\] for
  $f_1, \dots, f_n \in K[x_1, \dots, x_n]$.
  Then we call $f$ a polynomial map over $K$.

  For the sake of computation it is simpler to simply assert
  the data of the $n$ polynomials directly:

  \begin{lstlisting}
  def poly_map (K : Type*) [comm_semiring K] (n : ℕ) : Type* :=
  fin n → mv_polynomial (fin n) K \end{lstlisting}

  Then we can take the map on types/sets by evaluating each polynomial
  \begin{lstlisting}
  def eval : poly_map K n → (fin n → K) → (fin n → K) :=
  λ ps as k, mv_polynomial.eval as (ps k) \end{lstlisting}
\end{dfn}

\begin{prop}[Ax-Grothendieck]
    \link{ax_groth_thm}
    Any injective polynomial map over an algebraically closed field is surjective.
    In particular injective polynomial maps over $\C$ are surjective.

\begin{lstlisting}
  theorem Ax_Groth {n : ℕ} {ps : poly_map K n}
    (hinj : function.injective (poly_map.eval ps)) :
  function.surjective (poly_map.eval ps) := sorry \end{lstlisting}
\end{prop}

The key lemma to proving this is the \linkto{lefschetz}{Lefschetz principle},
which (informally) says that ring theoretic statements are true in particular models
of algebraically closed fields if and only if they are true in all algebraically closed fields
(assuming zero or large enough prime characteristic).

An overview of the proof of Ax-Grothendieck follows:
\[\begin{tikzcd}[column sep=large]
	{\text{(algebraic) $\chi_{p}$ Ax-G}} \\
	{\text{(model th.) $\chi_{p}$ Ax-G}} \\
	{\text{(model th.) }\chi_p \text{ Ax-G}} & {\text{(algebraic) }\chi_p \text{ Ax-G}} \\
	{\text{(model th.) }\chi_0 \text{ Ax-G}} & {\text{(algebraic) }\chi_0 \text{ Ax-G}} && {\text{(algebraic) Ax-G}}
	\arrow["{\ACF_{p} \text{is complete}}", shift right=5, from=2-1, to=3-1]
	\arrow["{\text{Lefschetz }\chi\text{-change}}", shift right=5, from=3-1, to=4-1]
  \arrow["{\text{soundness}}", shift right=5, from=1-1, to=2-1]
  \arrow["{\text{completeness}}"', from=3-1, to=3-2]
	\arrow["{\text{completeness}}"', from=4-1, to=4-2]
	\arrow[from=3-2, to=4-4]
	\arrow["{\text{case on }\chi}", from=4-2, to=4-4]
\end{tikzcd}\]

\begin{itemize}
  \item We make a model-theoretic statement of Ax-Grothendieck
        and show internal completeness and soundness.
  \item We intend to reduce to the finite characteristic case,
        hence we first case on characteristic.
  \item By internal completeness it suffices to show
        each case (stated model-theoretically).
  \item We apply \linkto{lefschetz}{Lefschetz}
        so that it suffices to only prove the prime characteristic case
        (stated model-theoretically).
  \item By \linkto{lefchetz}{Lefschetz} again ($\ACF_{p}$ is complete),
        it suffices to only prove the statement for a single model of $\ACF_{p}$,
        for which we will pick an algebraic closure of $\F_{p}$.
  \item Converting back using internal soundness, it suffices to prove
        the algebraic statement for an algebraic closure of $\F_{p}$.
        This will be a specialization of
        \linkto{ax_groth_locally_fin}{Ax-Grothendieck for locally finite fields}.
        Intuitively this says: locally the polynomial map restricts to an injection
        on finite affine varieties, and injectivity on a finite set is surjective.
\end{itemize}

\subsection{Writing down polynomials}

Our objective is to state Ax-Grothendieck model-theoretically.
To express ``for any polynomial map ...'',
which amounts to somehow quantifying over all polynomials in $n$ variables,
we bound the degrees of all the polynomials,
i.e. asking instead ``for any polynomial map with degree at most $d$''.
Then we can write the polynomial as a sum of its monomials,
with the coefficients as bounded variables.
Let us assume we have an $n$-variable polynomial $p \in K[x_{1},\dots,x_{n}]$.
We know that $p$ can be written as a sum of its monomials,
and the set of monomials \texttt{monom\_deg\_le n d} is finite,
depending on the degree $d$ of the polynomial $p$.
It can be indexed by

\[ \texttt{monom\_deg\_le\_finset n d} := \set{ f : \texttt{fin n} \to \N \st \sum_{i < n} f i \le d }\]

Then we write
\[ p = \sum_{f\texttt{ : monom\_deg\_le n d}} p_{f}\prod_{i < n} x^{f i}\]

The typical approach to writing a sum like this in \texttt{lean} would be
to tell lean that only finitely many of the $p_{f}$ are non-zero
($p_{\star}$ is finitely supported - \texttt{finsupp}).
However, the API built for this assumes that the underlying
type in which the sum takes place is a commutative monoid,
which is not the case here,
as we will be expressing the above as a sum of terms
in the language of rings.
This type has addition and multiplication and so on,
which we supplied as \linkto{lean_symbols_for_ring_symbols}{instances} already,
but these are neither commutative nor associative.
Thus the workaround here was to use \texttt{list.sumr}
(my own definition, similar to \texttt{list.sum}) instead,
which will take a list of terms in the language of rings, and sum them together.

The below definition is meant to (re)construct polynomials as described above,
using free variables to represent the coefficients of some polynomial.
This can then be used to express injectivity and surjectivity.

\begin{lstlisting}
def poly_indexed_by_monoms (n d s p c : ℕ)
  (hndsc : (monom_deg_le n d).length + s ≤ c)
  (hnpc : n + p ≤ c) :
  bounded_ring_term c :=
list.sumr
(list.map
  (
    λ f : (fin n → ℕ),
    let
      x_js : bounded_ring_term c :=
      x_ ⟨((monom_deg_le n d).index_of' f + s) , ... ⟩,
      x_ip (i : fin n) : bounded_ring_term c :=
      x_ ⟨ (i : ℕ) + p , ... ⟩
    in
    x_js * (n.non_comm_prod (λ i, npow_rec (f i) (x_ip i)))
  )
  (monom_deg_le n d)
) \end{lstlisting}

To explain the above, we wish to express the ring term with $c$ many free variables
(``in context $c$'')
\[\sum_{f \in \texttt{monom\_deg\_le n d}} x_{j+s} \prod_{0 \le i < n} x_{i+p}^{f(i)}\]
\begin{itemize}
  \item When we write \texttt{x\_ < n , ... >} we are giving a natural $n$
        and a proof that $n$ is less than the context/variable bound $c$,
        which we omit here.
  \item \texttt{list.map} takes the list \texttt{monom\_deg\_le n d}
        (which is just \texttt{monom\_deg\_le\_finset n d} as a list instead\footnote{
          This uses the axiom of choice, in the form of \texttt{finset.to\_list}.})
        and gives us a list of terms looking like
        \[x_{js} \prod_{i < n} (x_{ip} i) ^{f i}\] one for each
        $f \in \texttt{monom\_deg\_le\_finset n d}$.
  \item Then \texttt{list.sumr} sums these terms together,
        producing a term in $c$ many free variables representing a polynomial.
  \item To define \texttt{x\_js} we take the index of $f$ in the list that $f$ came from
        and we add $s$ at the end and take the variable $x_{\texttt{index f} + s}$.
  \item To make the product we use \texttt{non\_comm\_prod}
        (this makes products indexed by \texttt{fin n},
        and works without commutativity or associativity conditions).
        For each $i < n$ we multiply together $x_{i + p}$.
  \item The purpose of adding $s$ and $p$ is to ensure we are not repeating variables in
        this expression. They give us control of where the variables begin and end.

        In the two situations where these polynomials are used $p$ is taken to be
        either $0$ or $n$; this makes the realizing variables $x_{0},\dots,x_{n - 1}$
        or $x_{n}, \dots, x_{2n - 1}$ represent evaluating the polynomials at values
        assigned to $x_{0},\dots,x_{2n - 1}$.

        The value $s$ in both instances taken to be
        $j \times \abs{\texttt{monom\_deg\_le\_finset n d}} + 2n$, where
        $j$ will represent the $j$-th polynomial
        (out of the $n$ polynomials from \texttt{poly\_map\_data}).
        This ensures that the variables between different polynomials
        in our polynomial map don't overlap.
\end{itemize}

\subsection{Injectivity and surjectivity}

We can then express injectivity of a polynomial map.

\begin{lstlisting}
def inj_formula (n d : ℕ) :
  bounded_ring_formula (n * (monom_deg_le n d).length) :=
let monom := (monom_deg_le n d).length in
-- for all pairs in the domain x₋ ∈ Kⁿ and ...
bd_alls' n _
$
-- ... y₋ ∈ Kⁿ
bd_alls' n _
$
-- if at each pⱼ
(bd_big_and n
-- pⱼ x₋ = pⱼ y₋
  (λ j,
    (poly_indexed_by_monoms n d (j * monom + n + n) n _ _ ) -- note n
    ≃
    (poly_indexed_by_monoms n d (j * monom + n + n) 0 _ _ ) -- note 0
  )
)
-- then
⟹
-- at each 0 ≤ i < n,
(bd_big_and n ( λ i,
-- xᵢ = yᵢ (where yᵢ is written as xᵢ₊ₙ₊₁)
  x_ ⟨ i + n , ... ⟩ ≃ x_ (⟨ i , ... ⟩)
))
\end{lstlisting}

To explain the above, suppose we have $p$ the data of a polynomial map
(i.e. for each $j < n$ we have $p_j$ a polynomial).
We wish to express ``for all $x,y \in K^{n}$,
if $p x = p y$ then $x = y$''.
\begin{itemize}
  \item \texttt{bd\_alls' n} adds $n$ many $\forall$s in front of the
        formula coming after.
        The first represents $x = (x_{n},\dots,x_{2n-1})$ and the second represents
        $y = (y_{0},\dots,y_{n-1}) = (x_{0},\dots,x_{n-1})$.
        We choose this ordering since when we quantify this expression
        we first introduce $x$, which is of a higher index.
  \item \texttt{bd\_big\_and n} takes $n$ many formulas and places $\AND$s between
        each of them. In particular it expresses $p x = p y$, by breaking this up
        into the data of ``for each $j < n$, $p_{j} x = p_{j} y$'',
        as well as $x = y$, by breaking this up into the data of ``for each $i < n$,
        $x_{i+n} = x_{i}$''
  \item To write $p_{j} x$ and $p_{j} y$ we simply find the right variable indices
        to supply \texttt{poly\_indexed\_by\_monoms},
        and we ask for them to be equal.
\end{itemize}

Surjectivity is similar

\begin{lstlisting}
def surj_formula (n d : ℕ) :
  bounded_ring_formula (n * (monom_deg_le n d).length) :=
let monom := (monom_deg_le n d).length in
-- for all x₋ ∈ Kⁿ in the codomain
bd_alls' n _
$
-- there exists y₋ ∈ Kⁿ in the domain such that
bd_exs' n _
$
-- at each 0 ≤ j < n
bd_big_and n
-- pⱼ y₋ = xⱼ
λ j,
  poly_indexed_by_monoms n d (j * monom + n + n) 0 _
    inj_formula_aux0 inj_formula_aux1
  ≃
  x_ ⟨ j + n , ... ⟩ \end{lstlisting}

We wish to express ``for all $x \in K^{n}$, there exists $y \in K^{n}$ such that $p y = x$''.
Just like \texttt{bd\_alls' n}, \texttt{bd\_exs' n} adds $n$ many $\exists$s in front of the
formula coming after.

Now we are ready to express Ax-Grothendieck model theoretically and state internal
soundness and completeness.
\begin{lstlisting}
/-- Ax-Grothendieck stated model-theoretically -/
def Ax_Groth_formula (n d : ℕ) : sentence ring_signature :=
-- quantify over n many (n-variable polynomials) called ps;
-- i.e. the data of a polynomial map
-- by quantifying over (n * monom_of_bounded_degree) monomial coefficients
bd_alls (n * ((monom_deg_le n d).length))
-- if the polynomial function is injective then it is surjective
(inj_formula n d ⟹ surj_formula n d)

theorem realize_Ax_Groth_formula {n : ℕ} :
  (∀ d : ℕ, Structure K ⊨ Ax_Groth_formula n d)
  ↔
  (∀ (ps : poly_map K n),
    function.injective (poly_map.eval ps) → function.surjective (poly_map.eval ps)) :=
sorry
\end{lstlisting}

\subsection{Internal completeness and soundness}

We first need internal completeness and soundness for injectivity and surjectivity,
We only discuss injectivity as the results for surjectivity are very similar.
For injectivity we actually need two results:
\begin{itemize}
  \item (\texttt{realize\_inj\_formula\_of\_ring})
    Given a ring $A$ and a polynomial map,
    $A$ (as a model of \texttt{ring\_theory})
    realizes the formula \texttt{inj\_formula} evaluated at the coefficients
    of a polynomial map if and only if the polynomial map over $A$ is injective.
  \item (\texttt{realize\_inj\_formula\_of\_model})
    Given a model $M$ of \texttt{ring\_theory} and a (huge) list
    (\texttt{dvector}) of coefficients (representing $n$ polynomials),
    $M$ realizes the formula
    \texttt{inj\_formula} evaluated at the coefficients of a polynomial map
    if and only if the polynomial map over $M$ (as a field) is injective.
\end{itemize}

Clearly these are slightly different lemmas.
They are necessary because of the data one has at hand
depends on the direction one is working on.
In more detail one direction says

\begin{lstlisting}
lemma realize_inj_formula_of_ring
  {n d : ℕ}
  (ps : poly_map A n)
  (hdeg : ∀ (i : fin n), (ps i).total_degree ≤ d) :
  @realize_bounded_formula _ (struc_to_ring_struc.Structure A)
    _ _ (@poly_map.coeffs_dvector' A _ n d ps)
    (inj_formula n d) dvector.nil
  ↔
  function.injective (poly_map.eval ps) := sorry \end{lstlisting}

The function \texttt{poly\_map.coeffs\_dvector'} takes the polynomial \texttt{ps}
and finds all the coefficients of each polynomial in \texttt{ps}
and converts that to a \texttt{dvector}.
This is the only way we can use the data of \texttt{ps} in terms of realization.
Conversely all we have access to on the side of ring structures
will be a list of the coefficients, so we need

\begin{lstlisting}
lemma realize_inj_formula_of_model
  {n d : ℕ} (coeffs : dvector ↥(struc_to_ring_struc.Structure A)
    (n * (monom_deg_le n d).length)) :
  function.injective
    (poly_map.eval (λ i : fin n, mv_polynomial_of_coeffs (dvector.ith_chunk i coeffs)))
  ↔
  realize_bounded_formula coeffs (inj_formula n d) dvector.nil := sorry
\end{lstlisting}

Since all of these proofs are quite similar and unenlightening,
I will give an overview of one direction of the proof of
\texttt{realize\_inj\_formula\_of\_ring} and leave the rest.

We do some preliminary simplification and focus on the forward direction.
We want to show that \texttt{xs} and \texttt{ys} of type \texttt{fin n $\to$ A} are equal,
assuming that they are equal under evaluation through polynomial map \texttt{ps}.

\begin{lstlisting}
lemma realize_inj_formula_of_ring
  {n d : ℕ}
  (ps : poly_map A n)
  (hdeg : ∀ (i : fin n), (ps i).total_degree ≤ d) :
  @realize_bounded_formula _ (struc_to_ring_struc.Structure A)
    _ _ (@poly_map.coeffs_dvector' A _ n d ps)
    (inj_formula n d) dvector.nil
  ↔
  function.injective (poly_map.eval ps) :=
begin
  simp only [...],
  split,
    -- forward implication
  { intros hInj xs ys himage, \end{lstlisting}

We then make \texttt{xs} and \texttt{ys} into \texttt{dvector}s
in the obvious way,
and look to show that these are equal under evaluation
of \texttt{ps} (via \texttt{poly\_indexed\_by\_monos}).

\begin{lstlisting}
    set xs' : dvector (struc_to_ring_struc.Structure A) n := dvector.of_fn xs with hxs',
    set ys' : dvector (struc_to_ring_struc.Structure A) n := dvector.of_fn ys with hys',
    have hImage :  ∀ (k : fin n),
      realize_bounded_formula (ys'.append (xs'.append (poly_map.coeffs_dvector' d ps)))
        (poly_indexed_by_monoms n d (↑k * (monom_deg_le n d).length + n + n) n (n * (monom_deg_le n d).length + n + n)
             inj_formula_aux0
             inj_formula_aux2 ≃
           poly_indexed_by_monoms n d (↑k * (monom_deg_le n d).length + n + n) 0 (n * (monom_deg_le n d).length + n + n)
             inj_formula_aux0
             inj_formula_aux1)
        dvector.nil,
    { intro j,
      simp only [poly_map.eval] at himage,
      have himagej := congr_fun himage j,
      simp [...],
      convert himagej,
      { convert symm (eval_poly_map_xs ps xs' ys' hdeg j),
        funext k, simp [...], },
      { convert symm (eval_poly_map_ys ps xs' ys' hdeg j),
        funext k, simp [...], }, }, \end{lstlisting}

The above is a result of the assumption \texttt{himage} of equality
under \texttt{polynomial\_map.eval ps}.
Then it is a matter of applying injectivity (on the model theoretic side)

\begin{lstlisting}
    funext i, -- for each input (... they are equal)
    have hPreimage := hInj xs' ys' hImage i,
    simp [...] at hPreimage,
    convert hPreimage,
    {rw [hxs', dvector.nth_of_fn, fin.eta] },
    {rw [hys', dvector.nth_of_fn, fin.eta] }, },
  -- backward implication
  { ... },
end \end{lstlisting}

We can conclude use the internal soundness and completeness lemmas for
injectivity and surjectivity to show internal soundness and completeness
for \texttt{Ax\_Groth\_formula}:

\begin{lstlisting}
theorem realize_Ax_Groth_formula {n : ℕ} :
  (∀ d : ℕ, Structure K ⊨ Ax_Groth_formula n d)
  ↔
  (∀ (ps : poly_map K n),
    function.injective (poly_map.eval ps) → function.surjective (poly_map.eval ps)) :=
begin
  split,   \end{lstlisting}

Assume the structure satisfies \texttt{Ax\_Groth\_formula d} for all $d$ and
suppose we have a polynomial map \texttt{ps}.
We can pick $d$ to be \texttt{max\_total\_deg ps},
which is the maximum total degree across polynomials \texttt{ps i},
for all $i < n$.
We extract the monomial coefficients from \texttt{ps} as a \texttt{dvector}
called \texttt{coeffs}.

\begin{lstlisting}
  { intros H ps,
    specialize H (max_total_deg ps),
    let coeffs := poly_map.coeffs_dvector' d ps, \end{lstlisting}

Using internal completeness and soundness for injectivity and surjectivity
we can convert the goal from
\begin{lstlisting}
  ⊢ function.injective ps.eval → function.surjective ps.eval \end{lstlisting}
to
\begin{lstlisting}
  ⊢ realize_bounded_formula (poly_map.coeffs_dvector' coeffs ps)
    (inj_formula n (max_total_deg (λ (i : fin n), ps i)))
    dvector.nil →
  realize_bounded_formula (poly_map.coeffs_dvector' coeffs ps)
    (surj_formula n (max_total_deg (λ (i : fin n), ps i)))
    dvector.nil \end{lstlisting}

Then hypothesis $H$ is exactly what we need after simplification.

\begin{lstlisting}
    rw ← realize_surj_formula_of_ring ps (total_deg_le_max_total_deg ps),
    rw ← realize_inj_formula_of_ring ps (total_deg_le_max_total_deg ps),
    simp only [realize_sentence_bd_alls, Ax_Groth_formula] at H,
    exact H coeffs, }, \end{lstlisting}

The converse is similar
\begin{lstlisting}
  { intros H d,
    simp only [Ax_Groth_formula, realize_sentence_bd_alls],
    intro coeffs,
    simp only [realize_bounded_formula_imp, ← realize_surj_formula_of_model],
    intro hinj,
    apply H,
    rw realize_inj_formula_of_model,
    exact hinj, },
end
\end{lstlisting}

\subsection{The Lefschetz Principle}

To talk about Lefschetz it is sensible to first make the following definitions:

\begin{dfn}[Maximal theories and complete theories]
    \link{dfn_complete_theory}
    An $L$-theory $T$ is \textit{complete}
    when either of the following equivalent definitions hold:
    \begin{itemize}
      \item  $T$ deduces any sentence or its negation
    \begin{lstlisting}
      def is_complete' (T : Theory L) : Prop :=
      ∀ (ϕ : sentence L), T ⊨ ϕ ∨ T ⊨ ∼ ϕ \end{lstlisting}
      \item Sentences true in any model are deduced by the theory.
    \begin{lstlisting}
      def is_complete'' (T : Theory L) : Prop :=
      ∀ (M : Structure L) (hM : nonempty M) (ϕ : sentence L),
        M ⊨ T → M ⊨ ϕ → T ⊨ ϕ \end{lstlisting}
      \item All models of $T$ satisfy the same sentences
            (``are elementarily equivalent'').
    \end{itemize}

    Another definition \code{is\_complete} from the flypitch project
    is stronger than these conditions, and is useful when constructing
    theories with nice properties.
    We will not need this for now, but it is relevant to \linkto{henkinization}{Henkinization}.
    \begin{lstlisting}
      def is_complete (T : Theory L) :=
      is_consistent T ∧ ∀(f : sentence L), f ∈ T ∨ ∼ f ∈ T \end{lstlisting}
    I will refer to \code{is\_complete} as ``maximal consistent''\footnote{
      Given an \code{is\_complete'} theory that is also consistent,
      there is an obvious way of extending it to a maximal consistent theory.}.
\end{dfn}
\begin{proof}
  We show
\begin{lstlisting}
  lemma is_complete''_iff_is_complete' {T : Theory L} :
    is_complete' T ↔ is_complete'' T := sorry \end{lstlisting}
  The forward direction involves casing on the hypothesis of $T \vDash \phi$
  or $T \vDash \neg \phi$, in the first case we are done,
  and in the second we get a contradiction by
  $\phi$ being both true and false in our model $M$.
\begin{lstlisting}
  { intros H M hM ϕ hMT hMϕ,
    cases H ϕ with hTϕ hTϕ,
    { exact hTϕ },
    { have hbot := hTϕ hM hMT,
      rw realize_sentence_not at hbot,
      exfalso,
      exact hbot hMϕ } },
\end{lstlisting}
    On the other hand we need to case on whether $T$
    is consistent or not.
    When $T$ is consistent we can show $T$ deduces
    $\phi$ or its negation by checking in that model,
    otherwise $T$ should deduce anything.
\begin{lstlisting}
  { intros H ϕ,
    by_cases hM : ∃ M : Structure L, nonempty M ∧ M ⊨ T,
    { rcases hM with ⟨ M , hM0 , hMT ⟩,
      by_cases hMϕ : M ⊨ ϕ,
      { left, exact H M hM0 ϕ hMT hMϕ },
      { right,
        rw ← realize_sentence_not at hMϕ,
        exact H M hM0 _ hMT hMϕ} },
    { left,
      intros M hM0 hMT,
      exfalso,
      apply hM ⟨ M , hM0 , hMT ⟩} } \end{lstlisting}
\end{proof}

\begin{prop}[Lefschetz principle]
    \link{lefschetz}
    Let $\phi$ be a sentence in the language of rings.
    Then the following are equivalent:
    \begin{enumerate}
        \item Some model of $\ACF_0$ satisfies $\phi$.
        (If you like $\C \vDash \phi$.)
        \item $\ACF_0 \vDash \phi$
        \item There exists $n \in \N$ such that for any prime $p$
            greater than $n$, $\ACF_p \vDash \phi$
        \item There exists $n \in \N$ such that for any prime $p$
        greater than $n$, some model of $\ACF_p$ satisfies $\phi$.
    \end{enumerate}
    ($1. \IFF 2.$) and ($3. \IFF 4.$) are due to the theories $\ACF_{p}$
    being \linkto{dfn_complete_theory}{complete} for any $p$ ($0$ or prime).
\end{prop}

This will be proven in a \linkto{lefschetz_proof}{later section}.
For now we name the relevant lemmas that we need:

\begin{lstlisting}
theorem is_complete''_ACF₀ : is_complete'' ACF₀ := sorry

theorem characteristic_change (ϕ : sentence ring_signature) :
ACF₀ ⊨ ϕ ↔ (∃ (n : ℕ), ∀ {p : ℕ} (hp : nat.prime p), n < p → ACFₚ hp ⊨ ϕ) := sorry

theorem is_complete''_ACFₚ {p : ℕ} (hp : nat.prime p) : is_complete'' (ACFₚ hp) := sorry
\end{lstlisting}

\subsection{The Locally Finite Case}
\input{src/locally_finite.tex}

\subsection{Proving Ax-Grothendieck}

We conclude with a proof of Ax-Grothendieck.

\begin{proof}
By \linkto{ax_groth_locally_fin}{Ax-Grothendieck for locally finite fields},
the theorem holds for an algebraic closure of $\F_{p}$.
\begin{lstlisting}
lemma Ax_Groth_algebraic_closure_zmod (p : ℕ) [hp : fact (nat.prime p)] {n} :
  ∀ (ps : poly_map (algebraic_closure.of_ulift_zmod.{u} p) n),
    function.injective (poly_map.eval ps) → function.surjective (poly_map.eval ps) :=
Ax_Groth_of_locally_finite (ulift.{u} (zmod p))
    (algebraic_closure.is_algebraic (ulift.{u} (zmod p))) n \end{lstlisting}

By internal soundness (\code{realize\_Ax\_Groth\_formula}),
this implies that for any naturals $n$ and $d$,
an algebraic closure of $\F_{p}$ satisfies \code{Ax\_Groth\_formula n d}.

\begin{lstlisting}
lemma ACFₚ_ssatisfied_Ax_Groth_formula {p : ℕ} [hp : fact p.prime] (n : ℕ) :
  ∀ d, (ACFₚ hp.1) ⊨ Ax_Groth_formula n d :=
begin
  have h : ∀ d, (instances.algebraic_closure_of_zmod hp.1) ⊨ Ax_Groth_formula n d,
  { rw realize_Ax_Groth_formula,
    exact Ax_Groth_algebraic_closure_zmod p }, \end{lstlisting}

Since $\ACF_{p}$ is complete and an algebraic closure of $\F_{p}$ is a model,
\code{Ax\_Groth\_formula n d} is satisfied in any model of $\ACF_{p}$.

\begin{lstlisting}
  intro d,
  exact Lefschetz.is_complete''_ACFₚ hp.1
    (instances.algebraic_closure_of_zmod hp.1) ⟨ 0 ⟩ (Ax_Groth_formula n d)
    (instances.algebraic_closure_of_zmod_models_ACFₚ hp.1) (h d),
end \end{lstlisting}

Applying Lefschetz ($2. \IFF 3.$), this implies that \code{Ax\_Groth\_formula n d}
is satisfied in models $\ACF_{0}$.

\begin{lstlisting}
lemma ACF₀_ssatisfied_Ax_Groth_formula (n d : ℕ) :
  ACF₀ ⊨ Ax_Groth_formula n d :=
begin
  rw Lefschetz.characteristic_change,
  use 0,
  intros p hp _,
  haveI : fact p.prime := ⟨ hp ⟩,
  apply ACFₚ_ssatisfied_Ax_Groth_formula,
end \end{lstlisting}

Since any algebraically closed field $K$ of characteristic zero is a model of $\ACF_{0}$,
we have that it satisfies \code{Ax\_Groth\_formula n d} for all $n$ and $d$,
hence by internal completeness we see that Ax-Grothendieck is true for $K$.

\begin{lstlisting}
lemma realize_Ax_Groth_formula_of_char_zero
  (h0 : char_zero K) (n d : ℕ) :
  Rings.struc_to_ring_struc.Structure K ⊨ Ax_Groth_formula n d :=
@ACF₀_ssatisfied_Ax_Groth_formula n d (Structure K) ⟨ 0 ⟩
    is_alg_closed_to.realize_ACF₀

lemma Ax_Groth_char_zero (h0 : char_zero K) {n : ℕ} {ps : poly_map K n} :
  function.injective (poly_map.eval ps) → function.surjective (poly_map.eval ps) :=
realize_Ax_Groth_formula.mp (realize_Ax_Groth_formula_of_char_zero h0 _) _ \end{lstlisting}

We can also apply internal completeness with our work above to obtain

\begin{lstlisting}
lemma Ax_Groth_char_p (hchar : (ring_char K).prime) {n : ℕ} {ps : poly_map K n} :
  function.injective (poly_map.eval ps) → function.surjective (poly_map.eval ps) :=
... \end{lstlisting}

Finally, we combine these for the characteristic-free general theorem.

\begin{lstlisting}
theorem Ax_Groth {n : ℕ} {ps : poly_map K n} :
  function.injective (poly_map.eval ps) → function.surjective (poly_map.eval ps) :=
begin
  rcases char_p.char_is_prime_or_zero K (ring_char K) with hp | hp,
  { exact Ax_Groth_char_p hp },
  { apply Ax_Groth_char_zero,
    rw ring_char.eq_iff at hp,
    apply @char_p.char_p_to_char_zero _ _ hp },
end \end{lstlisting}
\end{proof}
